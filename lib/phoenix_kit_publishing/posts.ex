defmodule PhoenixKit.Modules.Publishing.Posts do
  @moduledoc """
  Post CRUD operations for the Publishing module.

  Handles creating, reading, updating, and trashing posts,
  as well as slug/version/language extraction and timestamp management.

  Posts are routing shells — versions are the source of truth for status,
  published_at, and metadata (featured_image, tags, seo, description).
  Content rows hold per-language title + body + url_slug.
  """

  require Logger

  alias PhoenixKit.Modules.Languages.DialectMapper
  alias PhoenixKit.Modules.Publishing
  alias PhoenixKit.Modules.Publishing.ActivityLog
  alias PhoenixKit.Modules.Publishing.Constants

  @status_published Constants.status_published()

  @timestamp_modes Constants.timestamp_modes()
  alias PhoenixKit.Modules.Publishing.DBStorage
  alias PhoenixKit.Modules.Publishing.Hashtags
  alias PhoenixKit.Modules.Publishing.LanguageHelpers
  alias PhoenixKit.Modules.Publishing.ListingCache
  alias PhoenixKit.Modules.Publishing.PubSub, as: PublishingPubSub
  alias PhoenixKit.Modules.Publishing.Shared
  alias PhoenixKit.Modules.Publishing.SlugHelpers
  alias PhoenixKit.Modules.Publishing.StaleFixer
  alias PhoenixKit.Utils.Date, as: UtilsDate

  # Suppress dialyzer false positives for pattern matches
  @dialyzer {:nowarn_function, create_post: 2}

  @max_timestamp_attempts 60

  @doc """
  Returns true when the given post is a DB-backed post (has a UUID).
  """
  @spec db_post?(map()) :: boolean()
  def db_post?(post), do: not is_nil(post[:uuid])

  @doc "Counts posts on a specific date for a group."
  @spec count_posts_on_date(String.t(), Date.t() | String.t()) :: non_neg_integer()
  def count_posts_on_date(group_slug, date) do
    group_slug
    |> list_times_on_date(date)
    |> length()
  end

  @doc "Lists time values for posts on a specific date."
  @spec list_times_on_date(String.t(), Date.t() | String.t()) :: [Time.t()]
  def list_times_on_date(group_slug, date) do
    date = if is_binary(date), do: Date.from_iso8601!(date), else: date

    group_slug
    |> DBStorage.list_posts_timestamp_mode(Constants.status_published(), date: date)
    |> Enum.map(&(Time.to_string(&1.post_time) |> String.slice(0, 5)))
    |> Enum.uniq()
    |> Enum.sort()
  rescue
    e ->
      Logger.warning(
        "[Publishing] list_times_on_date failed for #{group_slug}/#{date}: #{inspect(e)}"
      )

      []
  end

  @doc """
  Finds a published post by URL slug — the public routing path. Drafts
  do NOT resolve through this lookup; use `find_by_url_slug_any_version/3`
  for admin/self-healing flows that need to see drafts.
  """
  @spec find_by_url_slug(String.t(), String.t(), String.t()) ::
          {:ok, map()} | {:error, :not_found | :cache_miss}
  def find_by_url_slug(group_slug, language, url_slug) do
    case find_content_with_stale_retry(
           group_slug,
           language,
           url_slug,
           &DBStorage.find_by_url_slug/3
         ) do
      nil -> {:error, :not_found}
      content -> {:ok, db_content_to_post_map(content)}
    end
  end

  @doc """
  Finds a post by URL slug, INCLUDING unpublished drafts. Internal use only —
  the stale-language self-healing flow (`StaleFixer`) and slug-uniqueness
  collision checks (`SlugHelpers.url_slug_exists?`) need to surface drafts
  so they can normalize / reject duplicates before publish. Never call this
  from a public route handler.
  """
  @spec find_by_url_slug_any_version(String.t(), String.t(), String.t()) ::
          {:ok, map()} | {:error, :not_found | :cache_miss}
  def find_by_url_slug_any_version(group_slug, language, url_slug) do
    case find_content_with_stale_retry(
           group_slug,
           language,
           url_slug,
           &DBStorage.find_by_url_slug_any_version/3
         ) do
      nil -> {:error, :not_found}
      content -> {:ok, db_content_to_post_map(content)}
    end
  end

  @doc """
  Finds a post by a previous URL slug (for 301 redirects).
  """
  @spec find_by_previous_url_slug(String.t(), String.t(), String.t()) ::
          {:ok, map()} | {:error, :not_found | :cache_miss}
  def find_by_previous_url_slug(group_slug, language, url_slug) do
    case find_content_with_stale_retry(
           group_slug,
           language,
           url_slug,
           &DBStorage.find_by_previous_url_slug/3
         ) do
      nil -> {:error, :not_found}
      content -> {:ok, db_content_to_post_map(content)}
    end
  end

  defp find_content_with_stale_retry(group_slug, language, url_slug, finder)
       when is_function(finder, 3) do
    case finder.(group_slug, language, url_slug) do
      nil ->
        retry_stale_slug_lookup(group_slug, language, url_slug, finder)

      content ->
        content
    end
  end

  defp retry_stale_slug_lookup(group_slug, language, url_slug, finder) do
    legacy_language = legacy_base_language(language)

    with legacy when is_binary(legacy) <- legacy_language,
         legacy_content when not is_nil(legacy_content) <- finder.(group_slug, legacy, url_slug),
         %_{version: %{post: db_post}} <- legacy_content do
      StaleFixer.fix_stale_post(db_post)
      finder.(group_slug, language, url_slug)
    else
      _ -> nil
    end
  end

  defp legacy_base_language(language) when is_binary(language) do
    base_language = DialectMapper.extract_base(language)
    if base_language != language, do: base_language, else: nil
  end

  defp legacy_base_language(_), do: nil

  @doc """
  Lists posts for a given publishing group slug.

  Queries the database directly via DBStorage.
  The optional second argument is accepted for API compatibility but unused.
  """
  @spec list_posts(String.t(), String.t() | nil) :: [map()]
  def list_posts(group_slug, _preferred_language \\ nil) do
    DBStorage.list_posts_with_metadata(group_slug)
  end

  @doc "Lists posts filtered by status (e.g. 'trashed', 'published')."
  @spec list_posts_by_status(String.t(), String.t()) :: [map()]
  def list_posts_by_status(group_slug, status) do
    DBStorage.list_posts_with_metadata(group_slug, status)
  end

  @doc "Lists raw DB post records for a group, optionally filtered by status."
  @spec list_raw_posts(String.t(), String.t() | nil) :: [struct()]
  def list_raw_posts(group_slug, status \\ nil) do
    if status,
      do: DBStorage.list_posts(group_slug, status),
      else: DBStorage.list_posts(group_slug)
  end

  @doc """
  Creates a new post for the given publishing group using the current timestamp.
  """
  @spec create_post(String.t(), map() | keyword()) :: {:ok, map()} | {:error, any()}
  def create_post(group_slug, opts \\ %{}) do
    case create_post_in_db(group_slug, opts) do
      {:ok, post} = result ->
        ActivityLog.log_manual(
          "publishing.post.created",
          ActivityLog.actor_uuid(opts),
          "publishing_post",
          post[:uuid] || post[:db_uuid],
          %{
            "group_slug" => group_slug,
            "slug" => post[:slug],
            "mode" => to_string(post[:mode] || "")
          }
        )

        result

      other ->
        ActivityLog.log_failed_mutation(
          "publishing.post.created",
          ActivityLog.actor_uuid(opts),
          "publishing_post",
          nil,
          %{"group_slug" => group_slug}
        )

        other
    end
  end

  @doc """
  Reads a post by its database UUID.

  Resolves the UUID to a group slug and post slug, then delegates to `read_post/4`.
  Invalid version/language params gracefully fall back to latest/primary.
  """
  @spec read_post_by_uuid(String.t(), String.t() | nil, integer() | nil) ::
          {:ok, map()} | {:error, any()}
  def read_post_by_uuid(post_uuid, language \\ nil, version \\ nil) do
    # A malformed uuid raises Ecto.Query.CastError out of repo.get — the
    # admin PostShow/Preview LVs and public version paths reach here with
    # user-controlled identifiers, and the crash took the whole LV down
    # instead of flashing "Post not found". (The trash/restore path casts
    # first; the update path already whitelists CastError.)
    case Ecto.UUID.cast(post_uuid) do
      {:ok, _} -> do_read_post_by_uuid(post_uuid, language, version)
      :error -> {:error, :not_found}
    end
  end

  defp do_read_post_by_uuid(post_uuid, language, version) do
    case DBStorage.get_post_by_uuid(post_uuid, [:group]) do
      nil ->
        {:error, :not_found}

      db_post ->
        db_post = StaleFixer.fix_stale_post(db_post)
        group_slug = db_post.group.slug
        resolved_language = resolve_language_to_dialect(language)
        version_number = if version, do: normalize_version_number(version), else: nil

        if db_post.post_date && db_post.post_time do
          DBStorage.read_post_by_datetime(
            group_slug,
            db_post.post_date,
            db_post.post_time,
            resolved_language,
            version_number
          )
        else
          DBStorage.read_post(group_slug, db_post.slug, resolved_language, version_number)
        end
    end
  rescue
    e in [Ecto.QueryError, DBConnection.ConnectionError] ->
      Logger.warning("[Publishing] read_post_by_uuid failed for #{post_uuid}: #{inspect(e)}")
      {:error, :not_found}
  end

  @doc """
  Reads an existing post.

  For slug-mode groups, accepts an optional version parameter.
  If version is nil, reads the latest version.

  Reads from the database.
  """
  @spec read_post(String.t(), String.t(), String.t() | nil, integer() | nil) ::
          {:ok, map()} | {:error, any()}
  def read_post(group_slug, identifier, language \\ nil, version \\ nil) do
    read_post_from_db(group_slug, identifier, language, version)
  end

  @doc """
  Updates a post in the database.
  """
  @spec update_post(String.t(), map(), map(), map() | keyword()) ::
          {:ok, map()} | {:error, any()}
  def update_post(group_slug, post, params, opts \\ %{}) do
    # Normalize opts to map (callers may pass keyword list or map)
    opts_map = if Keyword.keyword?(opts), do: Map.new(opts), else: opts

    audit_meta =
      opts_map
      |> Shared.fetch_option(:scope)
      |> Shared.audit_metadata(:update)

    result = update_post_in_db(group_slug, post, params, audit_meta)

    case result do
      {:ok, updated_post} ->
        ListingCache.regenerate(group_slug)

        unless Map.get(opts_map, :skip_broadcast, false) do
          PublishingPubSub.broadcast_post_updated(group_slug, updated_post)
        end

        ActivityLog.log_manual(
          "publishing.post.updated",
          actor_uuid_for_log(opts_map, audit_meta),
          "publishing_post",
          updated_post[:uuid] || post[:uuid],
          %{
            "group_slug" => group_slug,
            "slug" => updated_post[:slug] || post[:slug],
            "language" => updated_post[:language] || post[:language]
          }
        )

      _ ->
        ActivityLog.log_failed_mutation(
          "publishing.post.updated",
          actor_uuid_for_log(opts_map, audit_meta),
          "publishing_post",
          post[:uuid],
          %{
            "group_slug" => group_slug,
            "slug" => post[:slug],
            "language" => post[:language]
          }
        )
    end

    result
  end

  # Activity-log actor preference: explicit opts > scope-derived audit
  # metadata. The audit_meta path keeps backwards compatibility with LV
  # callers that only pass scope today (C10 will switch them to opts).
  defp actor_uuid_for_log(opts_map, audit_meta) do
    ActivityLog.actor_uuid(opts_map) || audit_meta[:updated_by_uuid]
  end

  @doc """
  Restores a trashed post by UUID, clearing its trashed_at timestamp.

  Regenerates the group cache and broadcasts the update.
  Returns {:ok, post_uuid} on success or {:error, reason} on failure.
  """
  @spec restore_post(String.t(), String.t(), keyword() | map()) ::
          {:ok, String.t()} | {:error, term()}
  def restore_post(group_slug, post_uuid, opts \\ []) do
    case DBStorage.get_group_post_by_uuid(group_slug, post_uuid) do
      nil ->
        ActivityLog.log_failed_mutation(
          "publishing.post.restored",
          ActivityLog.actor_uuid(opts),
          "publishing_post",
          post_uuid,
          %{"group_slug" => group_slug, "reason" => "not_found"}
        )

        {:error, :not_found}

      db_post ->
        case DBStorage.update_post(db_post, %{trashed_at: nil}) do
          {:ok, _} ->
            ListingCache.regenerate(group_slug)
            PublishingPubSub.broadcast_post_updated(group_slug, %{uuid: db_post.uuid})

            ActivityLog.log_manual(
              "publishing.post.restored",
              ActivityLog.actor_uuid(opts),
              "publishing_post",
              db_post.uuid,
              %{"group_slug" => group_slug, "slug" => db_post.slug}
            )

            {:ok, post_uuid}

          {:error, reason} ->
            ActivityLog.log_failed_mutation(
              "publishing.post.restored",
              ActivityLog.actor_uuid(opts),
              "publishing_post",
              db_post.uuid,
              %{"group_slug" => group_slug, "slug" => db_post.slug}
            )

            {:error, reason}
        end
    end
  end

  @doc """
  Soft-deletes a post by UUID (sets trashed_at timestamp).

  Returns {:ok, post_uuid} on success or {:error, reason} on failure.
  """
  @spec trash_post(String.t(), String.t(), keyword() | map()) ::
          {:ok, String.t()} | {:error, term()}
  def trash_post(group_slug, post_uuid, opts \\ []) do
    case DBStorage.get_group_post_by_uuid(group_slug, post_uuid, [:group]) do
      nil ->
        ActivityLog.log_failed_mutation(
          "publishing.post.trashed",
          ActivityLog.actor_uuid(opts),
          "publishing_post",
          post_uuid,
          %{"group_slug" => group_slug, "reason" => "not_found"}
        )

        {:error, :not_found}

      db_post ->
        case DBStorage.trash_post(db_post) do
          {:ok, _} ->
            broadcast_id = db_post.uuid
            ListingCache.regenerate(group_slug)
            PublishingPubSub.broadcast_post_deleted(group_slug, broadcast_id)

            ActivityLog.log_manual(
              "publishing.post.trashed",
              ActivityLog.actor_uuid(opts),
              "publishing_post",
              db_post.uuid,
              %{"group_slug" => group_slug, "slug" => db_post.slug}
            )

            {:ok, post_uuid}

          {:error, reason} ->
            ActivityLog.log_failed_mutation(
              "publishing.post.trashed",
              ActivityLog.actor_uuid(opts),
              "publishing_post",
              db_post.uuid,
              %{"group_slug" => group_slug, "slug" => db_post.slug}
            )

            {:error, reason}
        end
    end
  end

  # Extract slug, version, and language from a path identifier
  # Handles paths like:
  #   - "post-slug" → {"post-slug", nil, nil}
  #   - "post-slug/en" → {"post-slug", nil, "en"}
  #   - "post-slug/v1/en" → {"post-slug", 1, "en"}
  #   - "group/post-slug/v2/am" → {"post-slug", 2, "am"}
  @spec extract_slug_version_and_language(String.t(), String.t() | nil) ::
          {String.t(), integer() | nil, String.t() | nil}
  def extract_slug_version_and_language(_group_slug, nil), do: {"", nil, nil}

  def extract_slug_version_and_language(group_slug, identifier) do
    parts =
      identifier
      |> to_string()
      |> String.trim()
      |> String.trim_leading("/")
      |> String.split("/", trim: true)
      |> drop_group_prefix(group_slug)

    case parts do
      [] ->
        {"", nil, nil}

      [slug] ->
        {slug, nil, nil}

      [slug | rest] ->
        # Extract version if present (v1, v2, v3, etc.)
        {version, rest_after_version} = Shared.extract_version_from_parts(rest)

        # Extract language from remaining parts
        language =
          rest_after_version
          |> List.first()
          |> case do
            nil -> nil
            <<>> -> nil
            lang_code -> lang_code
          end

        {slug, version, language}
    end
  end

  @doc false
  @spec read_back_post(String.t(), String.t(), map() | nil, String.t() | nil, integer() | nil) ::
          {:ok, map()} | {:error, any()}
  def read_back_post(group_slug, identifier, db_post, language, version_number) do
    Shared.read_back_post(group_slug, identifier, db_post, language, version_number)
  end

  # ===========================================================================
  # Private helpers
  # ===========================================================================

  # Converts a DBStorage content record (with preloaded version/post/group) to a post map
  defp db_content_to_post_map(content) do
    version = content.version
    post = version.post
    version_data = version.data || %{}

    %{
      slug: post.slug,
      url_slug: content.url_slug,
      language: content.language,
      # Routing fields, mirroring the cache shape (Mapper.to_listing_map/4) so a
      # 301 redirect built from this map resolves to the right canonical URL for
      # both slug- and timestamp-mode posts instead of guessing the mode.
      mode: post.mode,
      date: post.post_date,
      time: post.post_time,
      metadata: %{
        title: content.title,
        status: version.status,
        description: version_data["description"]
      }
    }
  end

  defp create_post_in_db(group_slug, opts) do
    case DBStorage.get_group_by_slug(group_slug) do
      nil ->
        {:error, :group_not_found}

      group ->
        do_create_post_in_db(group_slug, group, opts)
    end
  end

  defp do_create_post_in_db(group_slug, group, opts) do
    scope = Shared.fetch_option(opts, :scope)
    mode = Publishing.get_group_mode(group_slug)
    primary_language = LanguageHelpers.get_primary_language()
    now = UtilsDate.utc_now()

    # Resolve user UUID for audit
    created_by_uuid = Shared.resolve_scope_user_uuids(scope)

    # Generate slug for slug-mode groups
    slug_result =
      case mode do
        "slug" ->
          title = Shared.fetch_option(opts, :title)
          preferred_slug = Shared.fetch_option(opts, :slug)
          SlugHelpers.generate_unique_slug(group_slug, title || "", preferred_slug)

        _ ->
          {:ok, nil}
      end

    with {:ok, post_slug} <- slug_result do
      # Build post attributes — posts are routing shells only
      post_attrs = %{
        group_uuid: group.uuid,
        slug: post_slug,
        mode: mode,
        created_by_uuid: created_by_uuid
      }

      post_attrs = maybe_add_initial_timestamp(post_attrs, mode, now)

      repo = PhoenixKit.RepoHelper.repo()

      tx_result =
        create_post_with_timestamp_retry(%{
          repo: repo,
          post_attrs: post_attrs,
          mode: mode,
          group_slug: group_slug,
          opts: opts,
          primary_language: primary_language,
          created_by_uuid: created_by_uuid,
          post_slug: post_slug
        })

      with {:ok, db_post} <- tx_result,
           {:ok, post} <- read_back_created_post(group_slug, db_post, mode, primary_language) do
        ListingCache.regenerate(group_slug)
        PublishingPubSub.broadcast_post_created(group_slug, post)
        {:ok, post}
      end
    end
  end

  # Timestamp-mode posts have a UNIQUE INDEX on `(group_uuid, post_date,
  # post_time)`. `resolve_timestamp_in_transaction/3` searches for the
  # first free minute by reading `get_post_by_datetime/3` then inserting
  # — but the read-then-insert is not atomic. Two concurrent
  # `create_post` calls in the same minute both see "free", both try to
  # insert, one fails with a unique-constraint violation.
  #
  # We retry the WHOLE transaction (not just the insert) on a
  # constraint violation so the new attempt re-runs the timestamp
  # scan and picks the next free minute. `@max_timestamp_retries`
  # bounds the loop; slug-mode posts never hit this path because
  # their unique key is `(group_uuid, slug)` which is enforced by the
  # `SlugHelpers.generate_unique_slug/3` upstream check.
  @max_timestamp_retries 5

  # `args` bundles the eight invariant inputs (repo, post_attrs, mode,
  # group_slug, opts, primary_language, created_by_uuid, post_slug) —
  # only `attempt` changes across the recursion, so they travel as one
  # context map rather than nine positional params.
  defp create_post_with_timestamp_retry(args, attempt \\ 0)

  defp create_post_with_timestamp_retry(_args, attempt)
       when attempt >= @max_timestamp_retries do
    {:error, :timestamp_collision_unresolvable}
  end

  defp create_post_with_timestamp_retry(args, attempt) do
    %{
      repo: repo,
      post_attrs: post_attrs,
      mode: mode,
      group_slug: group_slug,
      opts: opts,
      primary_language: pl,
      created_by_uuid: cbu,
      post_slug: ps
    } = args

    result =
      repo.transaction(fn ->
        create_post_in_transaction(repo, post_attrs, mode, group_slug, opts, pl, cbu, ps)
      end)

    if mode == "timestamp" and timestamp_collision?(result) do
      Logger.warning(
        "[Publishing] Timestamp collision detected, retrying " <>
          "(attempt #{attempt + 1}/#{@max_timestamp_retries})"
      )

      create_post_with_timestamp_retry(args, attempt + 1)
    else
      result
    end
  end

  # The `(group_uuid, post_date, post_time)` unique-constraint violation lands
  # here as a changeset error after `repo.rollback`. Ecto puts the error under
  # the FIRST field of the `unique_constraint/3` list — `:group_uuid`, NOT
  # `:post_time` — so matching only the date/time keys meant this never fired and
  # concurrent same-minute creates surfaced "Group has already been taken". Match
  # the constraint NAME (like StaleFixer.slug_conflict?/1) so a real FK error or
  # the slug-uniqueness violation on the same `:group_uuid` key isn't mistaken
  # for a timestamp collision.
  defp timestamp_collision?({:error, %Ecto.Changeset{errors: errors}}) do
    Enum.any?([:group_uuid, :post_date, :post_time], fn key ->
      case Keyword.get(errors, key) do
        {_msg, opts} ->
          Keyword.get(opts, :constraint) == :unique and
            Keyword.get(opts, :constraint_name) ==
              "idx_publishing_posts_group_date_time_unique"

        _ ->
          false
      end
    end)
  end

  defp timestamp_collision?(_other), do: false

  defp create_post_in_transaction(
         repo,
         post_attrs,
         mode,
         group_slug,
         opts,
         primary_language,
         created_by_uuid,
         post_slug
       ) do
    final_attrs = resolve_timestamp_in_transaction(post_attrs, mode, group_slug)
    content = Shared.fetch_option(opts, :content) || ""

    with {:ok, db_post} <- DBStorage.create_post(final_attrs),
         {:ok, db_version} <-
           DBStorage.create_version(%{
             post_uuid: db_post.uuid,
             version_number: 1,
             status: "draft",
             created_by_uuid: created_by_uuid,
             data: initial_version_data(content)
           }),
         {:ok, _content} <-
           DBStorage.create_content(%{
             version_uuid: db_version.uuid,
             language: primary_language,
             title: Shared.fetch_option(opts, :title) || "",
             content: content,
             url_slug: post_slug
           }) do
      db_post
    else
      {:error, reason} -> repo.rollback(reason)
    end
  end

  # The body is the only source of tags, so a create that carries content has
  # to derive them too — the editor's first save would otherwise be what
  # "registers" the tags, and a post created with content in one shot (import,
  # API, fixtures) rendered its #hashtags as archive links while being missing
  # from those very archives.
  defp initial_version_data(content) do
    case Hashtags.extract(content) do
      [] -> %{}
      tags -> %{"tags" => tags}
    end
  end

  defp maybe_add_initial_timestamp(post_attrs, "timestamp", now) do
    # Stamp the post's date/time in the configured site time zone, NOT raw UTC.
    # Timestamp-mode post_date/post_time are pure Date/Time values shown as-is
    # (no display conversion), and an edit stores the editor's naive wall clock —
    # so stamping creation in UTC made a freshly-created post disagree with an
    # edited one about which day it lives under (L5). No-op when time_zone is "0".
    local_now = shift_to_site_timezone(now)
    date = DateTime.to_date(local_now)
    time = %Time{hour: local_now.hour, minute: local_now.minute, second: 0, microsecond: {0, 0}}
    Map.merge(post_attrs, %{post_date: date, post_time: time})
  end

  defp maybe_add_initial_timestamp(post_attrs, _mode, _now), do: post_attrs

  # Shift a UTC datetime by the configured site `time_zone` (integer-hour offset,
  # default "0"). Mirrors the offset the display/edit layers use, so create/edit/
  # display all agree on a timestamp post's wall clock. Bad/missing setting → UTC.
  #
  # The offset itself comes from Constants, which is also what decides when a
  # scheduled post goes live — a second copy of the reading here is how the
  # clock a post is STAMPED on drifts from the clock it is RELEASED on.
  defp shift_to_site_timezone(datetime) do
    DateTime.add(datetime, Constants.site_offset_seconds(), :second)
  end

  defp resolve_timestamp_in_transaction(post_attrs, "timestamp", group_slug) do
    {date, time} =
      find_available_timestamp(group_slug, post_attrs.post_date, post_attrs.post_time)

    %{post_attrs | post_date: date, post_time: time}
  end

  defp resolve_timestamp_in_transaction(post_attrs, _mode, _group_slug), do: post_attrs

  defp read_back_created_post(group_slug, db_post, "timestamp", language) do
    DBStorage.read_post_by_datetime(group_slug, db_post.post_date, db_post.post_time, language, 1)
  end

  defp read_back_created_post(group_slug, db_post, _mode, language) do
    DBStorage.read_post(group_slug, db_post.slug, language, 1)
  end

  defp read_post_from_db(group_slug, identifier, language, version) do
    # If identifier is a UUID, resolve via UUID lookup (handles both modes)
    if Shared.uuid_format?(identifier) do
      read_uuid_post_in_group(group_slug, identifier, language, version)
    else
      case Publishing.get_group_mode(group_slug) do
        "timestamp" ->
          read_post_from_db_timestamp(group_slug, identifier, language, version)

        _ ->
          read_post_from_db_slug(group_slug, identifier, language, version)
      end
    end
  end

  # Pin a UUID lookup to the requested group. read_post_by_uuid/3 resolves purely
  # by UUID, so without this `GET /<any-group>/<uuid>` would serve a post from a
  # DIFFERENT group under the wrong group's name + canonical URL (M6).
  defp read_uuid_post_in_group(group_slug, identifier, language, version) do
    case read_post_by_uuid(identifier, language, version) do
      {:ok, post} ->
        if post[:group] == group_slug, do: {:ok, post}, else: {:error, :not_found}

      other ->
        other
    end
  end

  defp read_post_from_db_timestamp(group_slug, identifier, language, version) do
    case Shared.parse_timestamp_path(identifier) do
      {:ok, date, time, inferred_version, inferred_language} ->
        final_language = resolve_language_to_dialect(language || inferred_language)
        final_version = version || inferred_version
        version_number = normalize_version_number(final_version)

        case DBStorage.read_post_by_datetime(
               group_slug,
               date,
               time,
               final_language,
               version_number
             ) do
          {:ok, _} = ok ->
            ok

          {:error, :not_found} ->
            retry_stale_timestamp_post_read(
              group_slug,
              date,
              time,
              final_language,
              version_number
            )
        end

      _ ->
        # Fallback: try as slug-based lookup
        read_post_from_db_slug(group_slug, identifier, language, version)
    end
  end

  defp read_post_from_db_slug(group_slug, identifier, language, version) do
    {post_slug, inferred_version, inferred_language} =
      extract_slug_version_and_language(group_slug, identifier)

    final_language = resolve_language_to_dialect(language || inferred_language)
    final_version = version || inferred_version
    version_number = normalize_version_number(final_version)

    case DBStorage.read_post(group_slug, post_slug, final_language, version_number) do
      {:ok, _} = ok ->
        ok

      {:error, :not_found} ->
        retry_stale_slug_post_read(group_slug, post_slug, final_language, version_number)
    end
  end

  defp retry_stale_slug_post_read(group_slug, post_slug, language, version_number) do
    with legacy_language when is_binary(legacy_language) <- legacy_base_language(language),
         {:ok, _legacy_post} <-
           DBStorage.read_post(group_slug, post_slug, legacy_language, version_number),
         db_post when not is_nil(db_post) <- DBStorage.get_post(group_slug, post_slug) do
      StaleFixer.fix_stale_post(db_post)
      DBStorage.read_post(group_slug, post_slug, language, version_number)
    else
      _ -> {:error, :not_found}
    end
  rescue
    e in [Ecto.QueryError, DBConnection.ConnectionError] ->
      Logger.warning(
        "[Publishing] retry_stale_slug_post_read failed for #{group_slug}/#{post_slug}: #{inspect(e)}"
      )

      {:error, :not_found}
  end

  defp retry_stale_timestamp_post_read(group_slug, date, time, language, version_number) do
    with legacy_language when is_binary(legacy_language) <- legacy_base_language(language),
         {:ok, _legacy_post} <-
           DBStorage.read_post_by_datetime(
             group_slug,
             date,
             time,
             legacy_language,
             version_number
           ),
         db_post when not is_nil(db_post) <-
           DBStorage.get_post_by_datetime(group_slug, date, time) do
      StaleFixer.fix_stale_post(db_post)
      DBStorage.read_post_by_datetime(group_slug, date, time, language, version_number)
    else
      _ -> {:error, :not_found}
    end
  rescue
    e in [Ecto.QueryError, DBConnection.ConnectionError] ->
      Logger.warning(
        "[Publishing] retry_stale_timestamp_post_read failed for #{group_slug}/#{date}/#{time}: #{inspect(e)}"
      )

      {:error, :not_found}
  end

  defp normalize_version_number(nil), do: nil

  defp normalize_version_number(v) when is_integer(v) and v > 0, do: v
  defp normalize_version_number(v) when is_integer(v), do: nil

  defp normalize_version_number(v) do
    case Integer.parse("#{v}") do
      {n, _} when n > 0 -> n
      _ -> nil
    end
  end

  # Resolves base language codes (de, en) to stored BCP-47 dialect codes (de-DE, en-US).
  # Content rows store full dialect codes, but URL paths use base codes.
  defp resolve_language_to_dialect(nil), do: nil

  # Resolution order:
  #   1. The code is itself an enabled language → use as-is.
  #   2. The code is a base ("en") and an enabled dialect shares that base
  #      ("en-GB") → use that dialect, preferring `get_primary_language/0`
  #      when several dialects share the base.
  #   3. The code is a base with no matching enabled dialect → fall back to
  #      `DialectMapper.base_to_dialect/1` (hard-coded default like en→en-US).
  #   4. The code is a full dialect not enabled → return as-is and let the
  #      caller's `resolve_content/2` fallback chain handle the miss.
  defp resolve_language_to_dialect(language) do
    enabled = LanguageHelpers.enabled_language_codes()

    ci_exact =
      Enum.find(enabled, fn code -> String.downcase(code) == String.downcase(language) end)

    cond do
      language in enabled ->
        language

      # Lowercase sibling-dialect URL codes ("en-gb") resolve to the stored
      # enabled code ("en-GB") — without this, the read fell through to the
      # as-is branch, missed the content row, and the fallback chain served
      # (or let the editor EDIT) the primary language under the en-gb name.
      ci_exact != nil ->
        ci_exact

      DialectMapper.extract_base(language) == language ->
        LanguageHelpers.resolve_dialect_for_base(language, enabled,
          prefer: LanguageHelpers.get_primary_language()
        ) || DialectMapper.base_to_dialect(language)

      true ->
        language
    end
  end

  # Finds the next available minute for a timestamp-mode post.
  # If the given date/time is already taken, bumps forward by one minute at a time.
  # Limited to 60 attempts to prevent unbounded recursion.
  defp find_available_timestamp(group_slug, date, time, attempts \\ 0)

  defp find_available_timestamp(_group_slug, date, time, @max_timestamp_attempts) do
    {date, time}
  end

  defp find_available_timestamp(group_slug, date, time, attempts) do
    # Trashed-INCLUSIVE check: the unique index includes trashed rows, so a
    # version that only looked at live posts would keep handing back a slot the
    # DB rejects, and the collision retry could never converge (see M2/M1).
    if DBStorage.timestamp_slot_taken?(group_slug, date, time) do
      bump_timestamp(group_slug, date, time, attempts)
    else
      {date, time}
    end
  end

  # Advance the candidate timestamp by one minute, rolling over to the next day
  # at midnight, and retry the availability check.
  defp bump_timestamp(group_slug, date, time, attempts) do
    total_seconds = time.hour * 3600 + time.minute * 60 + 60

    if total_seconds >= 86_400 do
      next_date = Date.add(date, 1)
      find_available_timestamp(group_slug, next_date, ~T[00:00:00], attempts + 1)
    else
      next_hour = div(total_seconds, 3600)
      next_minute = div(rem(total_seconds, 3600), 60)
      next_time = %Time{hour: next_hour, minute: next_minute, second: 0, microsecond: {0, 0}}
      find_available_timestamp(group_slug, date, next_time, attempts + 1)
    end
  end

  # Updates a post in the database.
  # Writes title + content to the content row, version-level metadata to version.data.
  defp update_post_in_db(group_slug, post, params, audit_meta) do
    db_post = find_db_post_for_update(group_slug, post)

    cond do
      is_nil(db_post) ->
        {:error, :not_found}

      post[:mode] in @timestamp_modes || db_post.mode == "timestamp" ->
        do_update_post_in_db(db_post, post, params, group_slug, nil, audit_meta)

      true ->
        # The slug used to be written here, before the transaction below — so a
        # save that failed on its content (an over-long title, say) still moved
        # the post to its new address. The UI reported a failed save while the
        # old URL had already stopped working, and the redirect that would have
        # covered it is recorded by the part that rolled back.
        desired_slug = Map.get(params, "slug", post.slug)
        do_update_post_in_db(db_post, post, params, group_slug, desired_slug, audit_meta)
    end
  rescue
    e ->
      if db_exception?(e) do
        Logger.warning("[Publishing] update_post_in_db DB error: #{inspect(e)}")
        {:error, :db_update_failed}
      else
        # Don't swallow programmer errors as a generic DB failure — surface them
        # (with a stacktrace) so real bugs aren't masked as "save failed" (L11).
        Logger.error(
          "[Publishing] update_post_in_db bug: " <> Exception.format(:error, e, __STACKTRACE__)
        )

        reraise(e, __STACKTRACE__)
      end
  end

  # True for the database-level exceptions an update can legitimately raise (so
  # they degrade to {:error, :db_update_failed}); everything else is a code bug.
  defp db_exception?(%Postgrex.Error{}), do: true
  defp db_exception?(%DBConnection.ConnectionError{}), do: true
  defp db_exception?(%Ecto.StaleEntryError{}), do: true
  defp db_exception?(%Ecto.ConstraintError{}), do: true
  defp db_exception?(%Ecto.Query.CastError{}), do: true
  defp db_exception?(_), do: false

  # Find the DB post record for update, using UUID, date/time, or slug as available
  defp find_db_post_for_update(group_slug, post) do
    cond do
      # If we have a UUID, use it directly (most reliable)
      post[:uuid] ->
        DBStorage.get_post_by_uuid(post[:uuid], [:group])

      # Timestamp-mode: use date/time
      post[:mode] in @timestamp_modes && post[:date] && post[:time] ->
        DBStorage.get_post_by_datetime(group_slug, post[:date], post[:time])

      # Slug-mode: use slug
      post[:slug] ->
        DBStorage.get_post(group_slug, post[:slug])

      true ->
        nil
    end
  end

  defp maybe_update_db_slug(db_post, desired_slug, _group_slug)
       when desired_slug == db_post.slug do
    {:ok, db_post.slug}
  end

  defp maybe_update_db_slug(db_post, desired_slug, group_slug) do
    with {:ok, valid_slug} <- SlugHelpers.validate_slug(desired_slug),
         false <- SlugHelpers.slug_exists?(group_slug, valid_slug),
         {:ok, _} <- DBStorage.update_post(db_post, %{slug: valid_slug}) do
      {:ok, valid_slug}
    else
      true ->
        {:error, :slug_already_exists}

      {:error, %Ecto.Changeset{} = changeset} ->
        Logger.warning("[Publishing] slug update changeset error: #{inspect(changeset.errors)}")

        if Keyword.has_key?(changeset.errors, :slug),
          do: {:error, :slug_already_exists},
          else: {:error, :db_update_failed}

      {:error, reason} ->
        Logger.warning("[Publishing] slug update failed: #{inspect(reason)}")
        {:error, reason}
    end
  end

  defp do_update_post_in_db(db_post, post, params, group_slug, desired_slug, audit_meta) do
    version_number = post[:version] || 1
    version = DBStorage.get_version(db_post.uuid, version_number)

    if version do
      language = post[:language] || LanguageHelpers.get_primary_language()
      post_metadata = post[:metadata] || %{}
      content = Map.get(params, "content", post[:content] || "")
      new_title = resolve_post_title(params, post, content)
      new_status = Map.get(params, "status", post_metadata[:status] || "draft")

      # Promote any legacy content.data keys that V2 stores at the version
      # level (description / featured_image_uuid / seo_title / excerpt). The
      # whitelist in `preserve_content_data` would otherwise wipe them on this
      # save; promotion runs once per legacy row and is logged via Activity.
      legacy_promotions = collect_legacy_content_promotions(version, language)

      write_ctx = %{
        language: language,
        new_title: new_title,
        content: content,
        params: params,
        post: post,
        db_post: db_post,
        audit_meta: audit_meta,
        legacy_promotions: legacy_promotions,
        desired_slug: desired_slug,
        group_slug: group_slug
      }

      with :ok <- validate_title_for_publish(language, new_status, new_title),
           {:ok, {db_post, final_slug}} <- persist_post_update(version, write_ctx) do
        log_legacy_metadata_promoted(legacy_promotions, version, language)
        read_updated_post(db_post, group_slug, final_slug, language, version_number)
      end
    else
      {:error, :not_found}
    end
  end

  # Persist the three coupled writes of a post update in ONE transaction.
  # `upsert_post_content/6` strips the legacy V1 keys (description,
  # featured_image_uuid, seo_title, excerpt) from the content row that
  # `update_version_defaults/4` then re-persists at the version level. Without the
  # transaction, a failure in between committed the wipe but not the promotion —
  # destroying those values permanently (M3).
  defp persist_post_update(version, ctx) do
    repo = PhoenixKit.RepoHelper.repo()

    repo.transaction(fn ->
      # Serialize with publish/unpublish/delete (they take this same FOR
      # UPDATE lock). Without it, save_writable_status's check raced a
      # concurrent publish (archiving the version it had just made live),
      # and the version.data merge below read-modify-wrote a
      # pre-transaction snapshot — parallel AI translation jobs lost each
      # other's tags and legacy promotions.
      DBStorage.lock_post_row!(repo, ctx.db_post.uuid)

      # Fresh read under the lock — the caller's struct predates it.
      version = DBStorage.get_version_by_uuid(version.uuid) || version

      with {:ok, final_slug} <- resolve_slug_in_tx(ctx),
           :ok <-
             upsert_post_content(
               version,
               ctx.language,
               ctx.new_title,
               ctx.content,
               ctx.params,
               ctx.post
             ),
           :ok <- update_version_defaults(version, ctx.params, ctx.post, ctx.legacy_promotions),
           {:ok, synced} <- maybe_sync_datetime_and_audit(ctx.db_post, ctx.params, ctx.audit_meta) do
        {synced, final_slug}
      else
        {:error, reason} -> repo.rollback(reason)
      end
    end)
  end

  # Timestamp-mode posts have no slug to move; slug-mode ones rename here so
  # the rename shares the fate of everything else in the save.
  defp resolve_slug_in_tx(%{desired_slug: nil}), do: {:ok, nil}

  defp resolve_slug_in_tx(ctx),
    do: maybe_update_db_slug(ctx.db_post, ctx.desired_slug, ctx.group_slug)

  @default_title Constants.default_title()

  defp validate_title_for_publish(language, @status_published, title)
       when title in ["", @default_title] do
    primary_language = LanguageHelpers.get_primary_language()

    if language == primary_language,
      do: {:error, :title_required},
      else: :ok
  end

  defp validate_title_for_publish(_language, _status, _title), do: :ok

  defp read_updated_post(db_post, group_slug, final_slug, language, version_number) do
    if db_post.mode == "timestamp" do
      DBStorage.read_post_by_datetime(
        group_slug,
        db_post.post_date,
        db_post.post_time,
        language,
        version_number
      )
    else
      DBStorage.read_post(group_slug, final_slug, language, version_number)
    end
  end

  defp resolve_post_title(params, post, _content) do
    post_metadata = post[:metadata] || %{}

    Map.get(params, "title") ||
      post_metadata[:title] ||
      Constants.default_title()
  end

  defp maybe_put(map, _key, nil), do: map
  defp maybe_put(map, key, value), do: Map.put(map, key, value)

  # Writes title + content + language + url_slug to the content row.
  # Content rows no longer carry status or featured_image_uuid — those live on the version.
  defp upsert_post_content(version, language, new_title, content, params, post) do
    existing_content = DBStorage.get_content(version.uuid, language)
    existing_url_slug = if existing_content, do: existing_content.url_slug
    existing_data = if existing_content, do: existing_content.data || %{}, else: %{}

    resolved_url_slug =
      case Map.fetch(params, "url_slug") do
        # A key that's present but empty means "leave it alone", not "clear
        # it". Taking it literally blanked the content row's slug AND filed
        # the old one as a previous slug, so the post lost its URL and gained
        # a redirect pointing at nothing. The editor always sends a string, so
        # this is about programmatic callers passing a partial map.
        {:ok, val} when val in [nil, ""] -> existing_url_slug
        {:ok, val} -> val
        :error -> existing_url_slug
      end

    # Content data only holds content-row-specific metadata (previous_url_slugs, etc.)
    content_data =
      existing_data
      |> preserve_content_data(params, post)
      |> record_previous_url_slug(existing_url_slug, resolved_url_slug)
      |> put_og_overrides(params)

    case DBStorage.upsert_content(%{
           version_uuid: version.uuid,
           language: language,
           title: new_title,
           content: content,
           url_slug: resolved_url_slug,
           data: content_data
         }) do
      {:ok, _} -> :ok
      {:error, reason} -> {:error, reason}
    end
  end

  # `_stale_fixer` is the merge-conflict recovery stash (StaleFixer's
  # discarded-body snapshots) — without it in the whitelist, the first edit
  # after a heal wiped the only copy of the discarded text.
  @content_only_data_keys ~w(previous_url_slugs updated_by_uuid custom_css og _stale_fixer)
  @og_override_form_keys ~w(og_title og_description og_image_uuid)
  @legacy_promotable_keys ~w(description featured_image_uuid seo_title excerpt)

  # Preserve content-row-specific data on save.
  #
  # Three keys are genuinely per-language and stay on the content row:
  #   * `previous_url_slugs` — old slugs for 301 redirects
  #   * `updated_by_uuid`    — last-editor audit per language
  #   * `custom_css`         — per-language custom CSS
  #
  # Four V1 keys (`description`, `featured_image_uuid`, `seo_title`,
  # `excerpt`) are now version-level in V2; they're promoted up to
  # `version.data` by `collect_legacy_content_promotions/2` BEFORE this
  # function runs and then dropped here. The promotion happens once per
  # legacy row and is logged via `ActivityLog.log/1`.
  defp preserve_content_data(existing_data, _params, _post) do
    Map.take(existing_data, @content_only_data_keys)
  end

  # When a content row's custom url_slug changes, record the OLD slug so the
  # 301-redirect machinery (find_by_previous_url_slug/3) can forward its stale
  # URLs to the new one. Without this writer the whole previous-slug system
  # consumed data nothing produced — renaming a url_slug 404'd every old URL.
  #
  # The new slug is dropped from the history (so reverting A->B->A can't leave a
  # previous-slug pointing at the current URL, which would 301-loop), and the
  # list is deduped, newest-first.
  defp record_previous_url_slug(data, old_slug, new_slug)
       when old_slug in [nil, ""] or old_slug == new_slug,
       do: data

  defp record_previous_url_slug(data, old_slug, new_slug) do
    previous = data["previous_url_slugs"] || []

    updated =
      [old_slug | previous]
      |> Enum.reject(&(&1 in [nil, "", new_slug]))
      |> Enum.uniq()

    Map.put(data, "previous_url_slugs", updated)
  end

  # Write the per-language OpenGraph override into content.data["og"] from the
  # flat editor form fields (og_title / og_description / og_image_uuid).
  #
  # Authoritative only when the save actually carries og_* fields (the editor's
  # meta form does). A blank field clears that override key; clearing all three
  # drops the "og" map entirely. Saves that don't include og_* fields at all
  # (AI translation, programmatic update_post callers) leave the existing
  # override untouched — it survives via @content_only_data_keys above.
  defp put_og_overrides(data, params) do
    if Enum.any?(@og_override_form_keys, &Map.has_key?(params, &1)) do
      og =
        %{}
        |> maybe_put_og("title", Map.get(params, "og_title"))
        |> maybe_put_og("description", Map.get(params, "og_description"))
        |> maybe_put_og("image_uuid", Map.get(params, "og_image_uuid"))

      if map_size(og) == 0, do: Map.delete(data, "og"), else: Map.put(data, "og", og)
    else
      data
    end
  end

  defp maybe_put_og(map, key, value) when is_binary(value) do
    case String.trim(value) do
      "" -> map
      trimmed -> Map.put(map, key, trimmed)
    end
  end

  defp maybe_put_og(map, _key, _value), do: map

  # Reads the current content row and returns a map of legacy V1 keys that
  # are present on `content.data` but absent from `version.data`. The caller
  # merges this into the version update so the values land at the version
  # level on the same save the content row gets wiped clean. Returns `%{}`
  # when there's nothing to promote (the steady-state path).
  defp collect_legacy_content_promotions(version, language) do
    existing_content = DBStorage.get_content(version.uuid, language)
    content_data = (existing_content && existing_content.data) || %{}
    version_data = version.data || %{}

    Enum.reduce(@legacy_promotable_keys, %{}, fn key, acc ->
      maybe_promote_key(acc, key, content_data, version_data)
    end)
  end

  defp maybe_promote_key(acc, key, content_data, version_data) do
    with {:ok, value} <- Map.fetch(content_data, key),
         false <- Map.has_key?(version_data, key) do
      Map.put(acc, key, value)
    else
      _ -> acc
    end
  end

  defp log_legacy_metadata_promoted(promotions, _version, _language) when promotions == %{},
    do: :ok

  defp log_legacy_metadata_promoted(promotions, version, language) do
    ActivityLog.log(%{
      action: "publishing.content.metadata_promoted",
      mode: "auto",
      resource_type: "publishing_content",
      resource_uuid: version.uuid,
      metadata: %{
        "language" => language,
        "version_uuid" => version.uuid,
        "promoted_keys" => Map.keys(promotions)
      }
    })

    :ok
  end

  @doc """
  Updates version.data with metadata like featured_image_uuid, description, seo_title, tags, etc.

  Version is the source of truth for all post metadata beyond title and body.
  Merges new values into existing version.data, preserving keys not present in
  the update. The optional `legacy_promotions` map is merged in BEFORE the
  user updates so legacy content.data values fall through unchanged when the
  user didn't touch them — the promotion path that pairs with
  `preserve_content_data`'s whitelist (see posts.ex `do_update_post_in_db`).
  """
  @spec update_version_defaults(struct(), map(), map(), map()) :: :ok | {:error, term()}
  def update_version_defaults(version, params, post, legacy_promotions \\ %{}) do
    existing_data = version.data || %{}
    post_metadata = post[:metadata] || %{}

    new_data =
      existing_data
      |> Map.merge(legacy_promotions)
      |> maybe_put_version_field("featured_image_uuid", Map.get(params, "featured_image_uuid"))
      |> maybe_put_version_field(
        "description",
        Map.get(params, "description", post_metadata[:description])
      )
      |> maybe_put_version_field("seo_title", Map.get(params, "seo_title"))
      |> maybe_put_version_field("tags", resolve_tags(version, params))
      |> maybe_put_version_field("category_uuids", resolve_category_uuids(params))
      |> maybe_put_version_field("excerpt", Map.get(params, "excerpt"))
      |> maybe_put_version_field("featured", normalize_featured(Map.get(params, "featured")))
      # Public `?v=N` browsing is gated on this and the mapper reads it, but no
      # write path existed — so the setting could never actually be turned on.
      |> maybe_put_version_field(
        "allow_version_access",
        normalize_featured(Map.get(params, "allow_version_access"))
      )
      |> put_audio_uuid(Map.get(params, "audio_uuid"))

    # Also update version-level status and published_at if provided.
    # "published" is NEVER written here — it is set atomically with
    # active_version_uuid by Versions.publish_version/4. Writing it here (a
    # separate transaction) let a save commit status=published while the paired
    # publish rolled back (e.g. empty primary title), leaving a post that reads
    # "published" with no active version — admin shows published, public 404s (M4).
    version_attrs =
      %{data: new_data}
      |> maybe_put(:status, save_writable_status(version, Map.get(params, "status")))
      |> maybe_put(:published_at, parse_published_at_from_params(params))

    case DBStorage.update_version(version, version_attrs) do
      {:ok, _} -> :ok
      {:error, reason} -> {:error, reason}
    end
  end

  defp maybe_put_version_field(data, _key, nil), do: data
  defp maybe_put_version_field(data, key, value), do: Map.put(data, key, value)

  # nil = the field wasn't submitted, leave it alone. "" = the picker was
  # cleared, so DROP the key instead of storing a blank every reader has to
  # special-case. Dropping is safe for this key specifically because
  # "audio_uuid" is not in @legacy_promotable_keys — for a promotable key
  # (e.g. featured_image_uuid) an absent key is the signal to re-promote the
  # legacy content-level value, so deleting would resurrect what the user
  # just cleared.
  defp put_audio_uuid(data, nil), do: data

  defp put_audio_uuid(data, value) when is_binary(value) do
    if String.trim(value) == "",
      do: Map.delete(data, "audio_uuid"),
      else: Map.put(data, "audio_uuid", value)
  end

  defp put_audio_uuid(data, _value), do: data

  # Tags ARE body hashtags (boss call 2026-07-28), and the body is their ONLY
  # source: a save carrying content re-derives the version's tags as the union
  # of hashtags across all of its language bodies (the just-saved row is
  # already upserted in this transaction, so a fresh read sees it); a save
  # without content leaves tags alone. A caller-supplied "tags" list is
  # deliberately ignored — a second way to set tags would let a post carry a
  # tag that appears nowhere in its prose, which is exactly the state the
  # post page can no longer display now that the chip row is gone.
  defp resolve_tags(version, params) do
    if is_binary(Map.get(params, "content")) do
      DBStorage.batch_load_contents([version.uuid])
      |> Map.get(version.uuid, [])
      |> Hashtags.extract_all()
    end
  end

  # Categories filed against this version. `nil` (key absent) leaves the
  # existing filing alone, so a save that doesn't carry the field — an
  # autosave from a context that never loaded the picker, a translation
  # write — can't quietly unfile a post. An empty list is a real answer:
  # somebody removed the last chip, and that has to stick.
  defp resolve_category_uuids(params) do
    case Map.get(params, "category_uuids") do
      list when is_list(list) -> list |> Enum.filter(&is_binary/1) |> Enum.uniq()
      _ -> nil
    end
  end

  # Normalizes the editor's "featured" checkbox into a boolean for version.data.
  # `nil` (key absent) is preserved so a save that doesn't carry the field leaves
  # the existing flag untouched; the editor always submits "true"/"false", so an
  # explicit uncheck writes `false` (maybe_put_version_field only skips nil).
  defp normalize_featured(nil), do: nil
  defp normalize_featured(value) when value in [true, "true", "on"], do: true
  defp normalize_featured(value) when value in [false, "false", "off", ""], do: false
  defp normalize_featured(_value), do: nil

  # Drop a "published" status so it is never written outside publish_version/4's
  # atomic transaction (see update_version_defaults/4). draft/archived/nil pass
  # through unchanged.
  defp deferred_publish_status(@status_published), do: nil
  defp deferred_publish_status(status), do: status

  # The same reservation, in the other direction.
  #
  # A save carries the whole form, and a form is a snapshot of what the page
  # knew when it loaded. Open a draft in two languages, publish from one, and
  # the other still holds `status => "draft"`; its next autosave writes that
  # back over the version the publish just made live. The post is then live by
  # its pointer and a draft by its status — the public page still serves it,
  # because the join follows the pointer, while the admin list says draft and
  # `stale_fixer` won't repair it (it only heals a missing pointer).
  #
  # Taking a version down is `unpublish_post/3`'s job, where it happens under
  # the post lock together with the pointer. So a plain save may set any status
  # on a version that isn't live, and none at all on the one that is.
  defp save_writable_status(version, status) do
    status = deferred_publish_status(status)

    if status && demotes_live_version?(version, status) do
      nil
    else
      status
    end
  end

  defp demotes_live_version?(%{uuid: version_uuid, post_uuid: post_uuid}, status)
       when status in ["draft", "archived"] do
    case DBStorage.get_post_by_uuid(post_uuid) do
      %{active_version_uuid: ^version_uuid} -> true
      _ -> false
    end
  end

  defp demotes_live_version?(_version, _status), do: false

  defp parse_published_at_from_params(params) do
    case Map.get(params, "published_at") do
      nil ->
        nil

      "" ->
        nil

      dt_string when is_binary(dt_string) ->
        case DateTime.from_iso8601(dt_string) do
          {:ok, dt, _} -> dt
          _ -> nil
        end

      dt ->
        dt
    end
  end

  # Combined post-row update: timestamp-mode date/time sync (when
  # `published_at` changed) + audit metadata (`updated_by_uuid`).
  # Issuing both as a single `update_post/2` halves the number of
  # round-trips per save (PR #2 review #6) and keeps the post row's
  # `updated_at` consistent across the two concerns.
  defp maybe_sync_datetime_and_audit(db_post, params, audit_meta) do
    attrs =
      %{}
      |> add_datetime_sync_attrs(db_post, params)
      |> maybe_put(:updated_by_uuid, audit_meta[:updated_by_uuid])

    if map_size(attrs) == 0 do
      {:ok, db_post}
    else
      case DBStorage.update_post(db_post, attrs) do
        {:ok, updated} -> {:ok, updated}
        {:error, reason} -> {:error, reason}
      end
    end
  end

  defp add_datetime_sync_attrs(attrs, %{mode: "timestamp"} = db_post, params) do
    case parse_published_at_from_params(params) do
      nil ->
        attrs

      %DateTime{} = dt ->
        new_date = DateTime.to_date(dt)
        new_time = %Time{hour: dt.hour, minute: dt.minute, second: 0, microsecond: {0, 0}}

        if new_date != db_post.post_date or new_time != db_post.post_time do
          attrs
          |> Map.put(:post_date, new_date)
          |> Map.put(:post_time, new_time)
        else
          attrs
        end
    end
  end

  defp add_datetime_sync_attrs(attrs, _db_post, _params), do: attrs

  # Only drop group prefix if there are more elements after it
  # This prevents dropping the post slug when it matches the group slug
  defp drop_group_prefix([group_slug | rest], group_slug) when rest != [], do: rest
  defp drop_group_prefix(list, _), do: list
end
